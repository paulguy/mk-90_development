    ; Some code and ideas taken from
    ; https://github.com/vladkorotnev/SMPEmu/blob/master/pdp/pdpfw.mac  and
    ; https://github.com/azya52/MK90/blob/master/TRex/trex.mac

	.ENABL AMA 	; Absolute addressing
	.ASECT		; Absolute section
    .RADIX 10

	.= 0		; Beginning of cartridge
    nop ; seems to be needed
    br loader

    .= 32 ; a bunch of interrupt vectors seem to be before here
scratch: ; this can be used as memory after things are loaded

    arg = scratch ; 162 bytes from here until the keyboard interrupt vector
    ; use as scratch space since the loader is not needed after initial bootup

	;	----------------------------------
	;	 OS Procedures and Vars
	;	----------------------------------

    PRINTATTRS = ^O34072
    PRINTNUM = ^O153360 ; arg. r3
    PRINT = ^O153414 ; arg. r1
    INITRAM = ^O153700
    INITTTY = ^O120536
    LCDINA = ^O164000
    LCDINB = ^O164002
    WAITCOMM = ^O174506
    SMPCMD = ^O164026
    SMPBAUD = ^O164022
    SMPCS = ^O164024
    SMPIO = ^O164020
    GETCH = ^O131120
    PUTCH = ^O116542

    BOOTBLOCK_SIZE = 512

    SCREEN_WIDTH_B = 30
    SCREEN_WIDTH_W = SCREEN_WIDTH_B/2
    SCREEN_SIZE_B = 960
    SCREEN_SIZE_W = SCREEN_SIZE_B/2
    SCREEN_HEIGHT = 64
    SCREEN_WIDTH = 120

loader:
	mov #endOfProgram, r4
	mov	#endOfRAM-endOfProgram, r3
loaderClearScreenLoop:
	clr	(r4)+
	sob	r3, loaderClearScreenLoop

    mov #16384, sp

	mov	#screen0, @#LCDINA
	mov #^B1000100011000110, @#LCDINB	;26Hz

	;Loader copyed from Game of Life app of Piotr Piatek
    ;Which I copied again from the T-Rex game
	; specify the SMP address
	mtps #^O340		;disable interrupts
	bis	#8, r5		;writing to the SMP
	mov	r5,(r1)
	mov	#^O240, @#SMPCMD	;Write Address
	jsr	pc,(r2)
	mov #BOOTBLOCK_SIZE/256, @#SMPIO	;high address byte
	jsr	pc,(r2)
	mov #BOOTBLOCK_SIZE, @#SMPIO	;high address byte
	jsr	pc, (r2)
	tst	@#SMPCMD
	jsr	pc, (r2)
	; load data
	mov	#endOfProgram-BOOTBLOCK_SIZE, r4		;number of bytes
	mov	#BOOTBLOCK_SIZE, r3
	mov	#^O320, @#SMPCMD	;Read Postincrement
	jsr	pc, (r2)
	bic	#8, r5		;reading from the SMP
	mov	r5, (r1)
	jsr	pc, (r2)
nxtb:	
	movb @#SMPIO, (r3)+	;read data byte from the SMP to the RAM
	jsr	pc, (r2)
	sob	r4, nxtb		;next data byte
	tst	@#SMPCMD
	jsr	pc, (r2)
	; start the loaded program
	mov	#^O000, -(sp)	;user mode, interrupts enable
	mov	#start, -(sp)
	rti

    .blkb 14 ; push the keyboard interrupt vector as far to the end as possible

keyIntHandle:
    mtps #^O340
    mov r0, -(sp)
    mov r1, -(sp)

    mov #SMPBAUD, r1
    mov #8, (r1)+ ;@#SMPBAUD
    mov r1, r0
    mov #^B11100010, (r1)+ ;@#SMPCS
    clr (r1);@#SMPCMD
waitGetKey:
    tstb (r0) ;@#SMPCS
    bpl waitGetKey
    mov (r1), @#inkey

    mov (sp)+, r1
    mov (sp)+, r0
    rti

    .= 200 ; Location of KBCU IV
    .word	keyIntHandle, 0000

drawBitmap:
    ; Draw a bitmap to the screen
    bcopy_data = arg ; pointer to data
    bcopy_w = arg+2 ; width in bytes
    bcopy_h = arg+4 ; height in lines
    bcopy_x = arg+6 ; X position
    bcopy_y = arg+8 ; Y position

    ; figure out if the bitmap starts on the top or bottom of the screen split
    mov @#bcopy_y, r0
    cmp #SCREEN_HEIGHT/2, r0
    blt bitmapStartsBottom

; bitmap starts at top
    ; figure out if the sprite crosses the split and by how much
    add @#bcopy_h, r0
    sub #SCREEN_HEIGHT/2, r0
    bge bitmapCrossesSplit

; bitmap is entirely on top
    mov @#bcopy_h, r1 ; get the full height of the sprite

    ; do bitmap copy for top of screen (low bits)
    jsr pc, copyBitmapTop

    rts pc

bitmapCrossesSplit:
    mov @#bcopy_h, r1
    sub r0, r1 ; r0 will have the number of lines over the split
    mov r0, @#bcopy_h ; save remaining since we don't need the old value, so r0 can be used

    ; do bitmap copy for top of screen
    jsr pc, copyBitmapTop

    mov @#bcopy_h, r1 ; restore remaining lines
bitmapStartsBottom:

    ; do bitmap copy for bottom of screen (high bits)

    rts pc

copyBitmapTop:
    ; r1 - height to copy

    bcopy_remaining_rows = scratch+10
    bcopy_remaining_columns = scratch+12
    bcopy_screen_pointer = scratch+14
    bcopy_temp = scratch+16

    ; save r1 because it'll be used with sob
    mov r1, @#bcopy_remaining_rows
    ; copy width so it can be iterated over
    mov @#bcopy_w, @#bcopy_remaining_columns

    ; calculate pointer to first screen byte
    mov @#bcopy_y, r3
    mul #SCREEN_WIDTH_B, r3 ; r2 doesn't work?  use r3
    add #screen0, r3

    mov @#bcopy_x, r2 ; get X offset and convert it to a byte offset by chopping off 3 bits
    asr r2
    asr r2
    asr r2
    asl r2 ; each 8 pixels on screen is 1 word apart
    add r2, r3 ; add X offset to screen start pointer

    mov r3, @#bcopy_screen_pointer ; save the screen pointer too

    mov @#bcopy_data, r0 ; get first pointer to bitmap

    mov @#bcopy_x, r2 ; get how shifted right the value is by keeping the low 3 bits
    bic #^B1111111111111000, r2
    movb r2, @#bcopy_temp ; save shift value low

    beq centerStripSimpleLoopTopStart ; take simple path if aligned

    dec @#bcopy_remaining_columns ; 1 less center strip if unaligned to bytes

    mov r2, r5
    ; get a bit mask to clear from the screen word
    mov #^B0000000011111111, r2
copyTopShift:
    asr r2
    sob r5, copyTopShift

    movb r2, @#bcopy_temp+1 ; save shift mask high
    mov @#bcopy_temp, r2 ; now r2 has the shift mask high and the shift value low

    mov r0, @#screen0+2
    rts pc

leftStripLoopTop:
    movb (r0), r4 ; get bitmap byte
    movb r2, r5 ; get shift amount
leftTopShift: ; shift bitmap bits right
    asr r4 
    sob r5, leftTopShift
    
    mov (r3), r5 ; get screen word

    swab r2 ; get mask low
    bicb r2, r5 ; clear bits
    swab r2 ; restore value low
    bisb r4, r5 ; set bitmap bits in to screen word

    mov r5, (r3) ; write back to screen

    add #SCREEN_WIDTH_B, r3 ; next screen row
    add @#bcopy_w, r0 ; next bitmap row, ran out of registers
    sob r1, leftStripLoopTop

    mov @#bcopy_data, r0 ; restore bitmap pointer in to r0
    ; bitmap pointer stays the same because the right side of the
    ; leftmost byte is used for the leftmost center column

    add #2, @#bcopy_screen_pointer ; next screen column
    mov @#bcopy_screen_pointer, r3 ; restore screen pointer

    mov @#bcopy_remaining_rows, r1 ; restore height

    cmp @#bcopy_remaining_columns, 0 ; skip center strips if there are none
    beq rightStripLoopTop

centerStripLoopTop:
    inc r0 ; get right byte first so r5 is free later

    movb (r0), r4 ; get right bitmap byte
    dec r0 ; point to left bitmap byte
    bic #^B1111111100000000, r4 ; clear high bits

    movb r2, r5 ; get shift amount (r5 is used here)
centerTopShift: ; shift bitmap bits
    asr r4 
    sob r5, leftTopShift

    movb (r0), r5 ; get left bitmap byte

    sub #8, r2 ; get how far left to shift
    neg r2
    ash r2, r5 ; shift byte left
    neg r2
    add #8, r2 ; restore right shift value

    bic #^B1111111100000000, r5 ; clear high bits

    bis r5, r4 ; combine left byte in to right byte

    mov (r3), r5 ; get screen word

    bic #^B0000000011111111, r5 ; clear low bits
    bis r4, r5 ; write bitmap in to screen word

    mov r5, (r3) ; write word back to screen

    add #SCREEN_WIDTH_B, r3 ; next screen row
    add @#bcopy_w, r0 ; next bitmap row
    sob r1, centerStripLoopTop

    inc @#bcopy_data ; next bitmap column
    mov @#bcopy_data, r0 ; restore bitmap pointer in to r0

    add #2, @#bcopy_screen_pointer ; next screen column
    mov @#bcopy_screen_pointer, r3 ; restore screen pointer

    mov @#bcopy_remaining_rows, r1 ; restore height

    dec @#bcopy_remaining_columns
    bne centerStripLoopTop ; loop until mo more center strips

rightStripLoopTop:
    movb (r0), r4 ; get bitmap byte
    ash r2, r4 ; shift byte left, ash ignores high bits?
    bic #^B1111111100000000, r4 ; clear high bits

    mov (r3), r5 ; get screen word

    swab r2 ; get mask low
    bicb r2, r5 ; clear bits
    swab r2 ; restore mask high
    bis r4, r5 ; set bitmap bits in to screen word

    mov r5, (r3) ; write back to screen

    add #SCREEN_WIDTH_B, r3 ; update screen pointer
    add @#bcopy_w, r0 ; update bitmap pointer
    sob r1, rightStripLoopTop

    br stripLoopReturn

centerStripSimpleLoopTopStart:
    mov @#bcopy_w, r2 ; may as well use a register during the loop
    ; r2 normally has the shift amount which isn't meaningful here
centerStripSimpleLoopTop:
    movb (r0), r4 ; get bitmap byte

    bic #^B1111111100000000, r4 ; clear high bits

    mov (r3), r5 ; get screen word

    bic #^B0000000011111111, r5 ; clear low bits
    bis r4, r5 ; write bitmap in to screen word

    mov r5, (r3) ; write word back to screen

    add #SCREEN_WIDTH_B, r3 ; next screen row
    add r2, r0 ; next bitmap row
    sob r1, centerStripSimpleLoopTop

    inc @#bcopy_data ; next bitmap column
    mov @#bcopy_data, r0 ; restore bitmap pointer in to r0

    add #2, @#bcopy_screen_pointer ; next screen column
    mov @#bcopy_screen_pointer, r3 ; restore screen pointer

    mov @#bcopy_remaining_rows, r1 ; restore height

    dec @#bcopy_remaining_columns
    bne centerStripSimpleLoopTop ; loop until mo more center strips

stripLoopReturn:
    rts pc

start:
    mov #^B1010101110101011, @#screen0+SCREEN_WIDTH_B
    mov #^B1010101110101011, @#screen0+SCREEN_WIDTH_B+2
    mov #^B1010101110101011, @#screen0+SCREEN_WIDTH_B+4
    mov #^B1010101110101011, @#screen0+SCREEN_WIDTH_B+6

mainloop:
    mov #64, @#bitmapx
    
loop:
    mov #bitmap, @#bcopy_data
    mov #2, @#bcopy_w
    mov #16, @#bcopy_h
    mov @#bitmapx, @#bcopy_x
    mov #2, @#bcopy_y

    mov @#bitmapx, @#screen0
    jsr pc, drawbitmap
    wait

    inc @#bitmapx
    cmp @#bitmapx, #SCREEN_WIDTH-16
    ble loop

    br mainloop

; byte reads proceed 1, 0, 3, 2, 5, 4, ....
bitmap:
    .word ^B1110000000000111
    .word ^B0001100000011000
    .word ^B0000010000100000
    .word ^B0000001001000000
    .word ^B0011001001001100
    .word ^B0011000110001100
    .word ^B0000000110000000
    .word ^B0000000110000000
    .word ^B0000000110000000
    .word ^B0000000110000000
    .word ^B0001100110011000
    .word ^B0011101001011100
    .word ^B1111001001001111
    .word ^B1100010000100011
    .word ^B0001100000011000
    .word ^B1110000000000111

.EVEN

endOfProgram: ; must not go beyond 10KB
; This area will be memory values.
; even if these get cut off by the 10KB memory card limit, they'll be initialized elsewhere anyway

; 2 screen pages
screen0:
    .blkb SCREEN_SIZE_B
screen1:
    .blkb SCREEN_SIZE_B

inkey:
    .word 0

; state variables
bitmapX:
    .word 0

endOfRAM: ; must not go beyond 16KB

; no idea if these work, they don't seem to print anything at least, but i'm probably misunderstanding something.
.IF GT endOfProgram-10240
.ERROR endOfProgram ; Program and data size extends beyond the 10KB SMP ROM size
.IFF
.PRINT endOfProgram ; Program and Data Size
.ENDC

.IF GT endOfRAM-16384
.ERROR endOfRAM ; Program, data and memory space extends beyond the 16KB RAM space
.IFF
.PRINT endOfRAM ; Program, Data and Memory Size
.ENDC
